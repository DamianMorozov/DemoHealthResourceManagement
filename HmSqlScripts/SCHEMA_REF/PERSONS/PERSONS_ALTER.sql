------------------------------------------------------------------------------------------------------------------------
-- PERSONS_ALTER
------------------------------------------------------------------------------------------------------------------------
SET NOCOUNT ON;
SET ANSI_NULLS ON;
SET QUOTED_IDENTIFIER ON;
SET TRANSACTION ISOLATION LEVEL READ COMMITTED;
PRINT N'[ ] SET TRANSACTION ISOLATION LEVEL READ COMMITTED';
DECLARE @DB_NAME_CUR VARCHAR(128) = NULL;
DECLARE @DB_NAME_DEV VARCHAR(128) = 'HRM_DEMO_DEV';
DECLARE @DB_NAME_PROD VARCHAR(128) = 'HRM_DEMO_PROD';
DECLARE @SCHEMA_NAME VARCHAR(128) = 'REF';
DECLARE @SCHEMA_ID INT = (SELECT [SCHEMA_ID] FROM [SYS].[SCHEMAS] WHERE [NAME] = @SCHEMA_NAME);
DECLARE @CMD NVARCHAR(MAX);
------------------------------------------------------------------------------------------------------------------------
DECLARE @IS_ACTION BIT = 1;
DECLARE @IS_COMMIT BIT = 1;
------------------------------------------------------------------------------------------------------------------------
IF (@IS_ACTION = 0) BEGIN
	PRINT N'[x] ACTION IS DISABLED';
END ELSE BEGIN
	BEGIN TRAN
	-- CHECK DB
	SET @DB_NAME_CUR = (SELECT DB_NAME());
	IF NOT (@DB_NAME_CUR = @DB_NAME_DEV OR @DB_NAME_CUR = @DB_NAME_PROD) BEGIN
		PRINT N'[x] CURRENT DB [' + @DB_NAME_CUR + '] IS NOT CORRECT';
	END ELSE BEGIN
		PRINT N'[✓] CURRENT DB [' + @DB_NAME_CUR + '] IS CORRECT';
		-- ALTER TABLE
		IF EXISTS (SELECT 1 FROM [SYS].[TABLES] WHERE [SCHEMA_ID] = @SCHEMA_ID AND [NAME] = N'PERSONS') BEGIN
			-- PRIMARY KEY
			IF NOT EXISTS (SELECT 1 FROM [INFORMATION_SCHEMA].[TABLE_CONSTRAINTS]
				WHERE [CONSTRAINT_TYPE] = 'PRIMARY KEY' AND [TABLE_SCHEMA] = @SCHEMA_NAME AND [TABLE_NAME] = 'PERSONS') BEGIN
				ALTER TABLE [REF].[PERSONS] ADD CONSTRAINT [PK_PERSONS_ID] PRIMARY KEY CLUSTERED ([ID] ASC) WITH 
					(PAD_INDEX = OFF, STATISTICS_NORECOMPUTE = OFF, IGNORE_DUP_KEY = OFF, ALLOW_ROW_LOCKS = ON, ALLOW_PAGE_LOCKS = ON) ON [FG_REF];
				PRINT N'[✓] PRIMARY KEY [PK_PERSONS_ID] WAS CREATED';
			END;
			-- DEFAULT
			IF NOT EXISTS (SELECT 1 FROM [SYS].[DEFAULT_CONSTRAINTS] WHERE [schema_id] = @SCHEMA_ID AND [NAME] = 'DF_PERSONS_DT_CREATE') BEGIN
				ALTER TABLE [REF].[PERSONS] ADD CONSTRAINT [DF_PERSONS_DT_CREATE] DEFAULT (GETDATE()) FOR [DT_CREATE];
				PRINT N'[✓] CONSTRAINT [DF_PERSONS_DT_CREATE] WAS CREATED';
			END;
			IF NOT EXISTS (SELECT 1 FROM [SYS].[DEFAULT_CONSTRAINTS] WHERE [schema_id] = @SCHEMA_ID AND [NAME] = 'DF_PERSONS_DT_CHANGE') BEGIN
				ALTER TABLE [REF].[PERSONS] ADD CONSTRAINT [DF_PERSONS_DT_CHANGE] DEFAULT (GETDATE()) FOR [DT_CHANGE];
				PRINT N'[✓] CONSTRAINT [DF_PERSONS_DT_CHANGE] WAS CREATED';
			END;
			-- CONSTRAINT
			IF NOT EXISTS (SELECT 1 FROM [INFORMATION_SCHEMA].[TABLE_CONSTRAINTS] WHERE [CONSTRAINT_NAME] = 'CK_PERSONS_GENDER') BEGIN
				ALTER TABLE [REF].[PERSONS] ADD CONSTRAINT [CK_PERSONS_GENDER] CHECK ([GENDER] = 'M' OR [GENDER] = 'F');
				ALTER TABLE [REF].[PERSONS] CHECK CONSTRAINT [CK_PERSONS_GENDER];
				PRINT N'[✓] CONSTRAINT [CK_PERSONS_GENDER] WAS CREATED';
			END;
			IF NOT EXISTS (SELECT 1 FROM [INFORMATION_SCHEMA].[TABLE_CONSTRAINTS] WHERE [CONSTRAINT_NAME] = 'CK_PERSONS_CELLPHONE') BEGIN
				-- 11 DIGITS | 79001234567 = +7(900)123-4567
				ALTER TABLE [REF].[PERSONS] ADD CONSTRAINT [CK_PERSONS_CELLPHONE] CHECK ([CELLPHONE] LIKE '[0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9]');
				ALTER TABLE [REF].[PERSONS] CHECK CONSTRAINT [CK_PERSONS_CELLPHONE];
				PRINT N'[✓] CONSTRAINT [CK_PERSONS_CELLPHONE] WAS CREATED';
			END;
			IF NOT EXISTS (SELECT 1 FROM [INFORMATION_SCHEMA].[TABLE_CONSTRAINTS] WHERE [CONSTRAINT_NAME] = 'CK_PERSONS_INN') BEGIN
				-- 12 DIGITS
				ALTER TABLE [REF].[PERSONS] ADD CONSTRAINT [CK_PERSONS_INN] CHECK ([INN] LIKE '[0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9]');
				ALTER TABLE [REF].[PERSONS] CHECK CONSTRAINT [CK_PERSONS_INN];
				PRINT N'[✓] CONSTRAINT [CK_PERSONS_INN] WAS CREATED';
			END;
		END;
	END;
	-- COMMIT
	IF (@IS_COMMIT = 1) BEGIN
		COMMIT TRAN;
		PRINT N'[✓] JOB WAS COMMITTED';
	END ELSE BEGIN
		ROLLBACK TRAN;
		PRINT N'[x] JOB WAS ROLLBACKED';
	END;
END;
------------------------------------------------------------------------------------------------------------------------
